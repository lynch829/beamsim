! File fobodobo_NL.lat
! 
! Written for the January, 2007 USPAS.
! To be used in conjunction with CHEF.
! 
! Send complaints to the author: Leo Michelotti
! 
! Add an RF cavity (EGS) 01/30/2009
!
! Update 07/14/2015 - Adding variations for non linear propagation tests - Nathan Cook
! 
! ------------------
! Parameters
! ------------------
twopi       :=   6.2831853072
n           :=   16                  !         : number of cells
momentum    := 10                    ! [GeV/c] : particle momentum; 
                                     !         :   arbitrary number
bendangle16   := twopi/(2*n)         ! [rad]   : dipole bend angle
bendangle32   := twopi/(2*2*n)       ! [rad]   : dipole bend angle
focus       :=   7                   ! [m]     : focal length of equivalent 
                                     !         :   thin quad
sepn        :=  10                   ! [m]     : distance between quad centers
quadlength  :=   0.2                 ! [m]     : quadrupole length
strength    := 1/(focus*quadlength)  ! [m**-2] : quadrupole strength
                                     !         :   = B'/brho, where
                                     !         :   brho = momentum/0.299792458
pct         :=   0.4                 !         : fraction of space between
                                     !         :   quads occupied by dipole
bendlength  := pct*(sepn-quadlength) ! [m]     : length of dipole

s6			:= 10					 !         : sextupole strength corrector
length6 := quadlength							 !    :sextupole length
strength6   := (strength*s6)*(quadlength/length6) !	  :sextupole strength

! bendlength := 0.4*(10-0.2) = 3.92
driftlength := (sepn-quadlength-bendlength)/2
! driftlenth := (10-0.2-3.92)/2 = 2.94
beam, particle=proton, pc=momentum
! harmonic number = 80  harmonic number, yields 2 meter wavelength
! the actual frequence is harmno following
harmno=128
lambda = 160.0/harmno

!hvoltage = 12500
hvoltage = 5.0

cavlen = 0.0 ! rf cavity length 1 meter, (half bucket length)
shortdlen = (driftlength - cavlen)/2.0   ! 0.97 m
shortdlen2 = (driftlength - length6)/2.0 !2 drifts per sextupole
! this lattice has 32 copies of a 20 m cell.

! ------------------
! Elements
! ------------------
o: drift,      l=driftlength
os: drift,      l=shortdlen
os2: drift,    l=shortdlen2
f: quadrupole, l=quadlength, k1=strength
d: quadrupole, l=quadlength, k1=(-strength)
b32: sbend,      l=bendlength, angle=bendangle32
b16: sbend,		l=bendlength, angle=bendangle16
r: rfcavity,	l=cavlen, volt=hvoltage, harmon=harmno, lag=0.5
s16p: sextupole, l=length6, k2=strength6
s16n: sextupole, l=length6, k2=(-strength6)

! ------------------
! Lattices
! ------------------
! Doesn't work: model:     line=( n*fobodobo )
fobodobo32:  line=( f, o, b32, o, d, o, b32, o )
fobrdobo32:  line=( f, o, b32, os, r, os, d, o, b32, o)
fobodobo16:  line=( f, o, b16, o, d, o, b16, o )
!sfobosdobo16: line =(s16p, f, o, b16, o, s16n, d, o, b16, o) 
sfobosdobo16: line =(s16p, f, os, b16, os, s16n, d, os, b16, os)

!model:     line=( fobrdobo32,31*fobodobo32 )
model32: line=(32*fobodobo32)
model16: line=(16*fobodobo16)
!model16s: line=(15*fobodobo16,sfobosdobo16)
model16s: line=(16*sfobosdobo16)